defaultTasks 'clean', 'test', 'aggregate'

repositories {
    mavenLocal()
    jcenter()
}

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.1.8")
        classpath("net.serenity-bdd:serenity-single-page-report:2.1.8")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'net.serenity-bdd.aggregator'

ext {
    slf4jVersion = '1.7.7'
    serenityCoreVersion = '2.1.8'
    serenityCucumberVersion = '2.1.2'
    junitVersion = '4.12'
    assertJVersion = '3.8.0'
    logbackVersion = '1.2.3'
    cucumberVersion = '4.8.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

/**
 * This is needed to make sure there are no Cucumber 2 dependencies in the classpath.
 */
configurations.all {
    resolutionStrategy {
        force "io.cucumber:cucumber-core:${cucumberVersion}"
    }
}

dependencies {
    compile "ch.qos.logback:logback-classic:${logbackVersion}"

    testCompile("net.serenity-bdd:serenity-core:${serenityCoreVersion}") {
        exclude group: "io.cucumber", module: "cucumber-core"
    }
    testCompile "io.cucumber:cucumber-java:${cucumberVersion}"
    testCompile "io.cucumber:cucumber-junit:${cucumberVersion}"

    testCompile "net.serenity-bdd:serenity-junit:${serenityCoreVersion}",
            "net.serenity-bdd:serenity-screenplay:${serenityCoreVersion}",
            "net.serenity-bdd:serenity-screenplay-webdriver:${serenityCoreVersion}",
            "net.serenity-bdd:serenity-ensure:${serenityCoreVersion}",
            "net.serenity-bdd:serenity-single-page-report:${serenityCoreVersion}",
            "net.serenity-bdd:serenity-cucumber4:${serenityCucumberVersion}",

            "junit:junit:${junitVersion}",
            "org.assertj:assertj-core:${assertJVersion}"
}

test {
    testLogging.showStandardStreams = true
    systemProperties System.getProperties()
}

gradle.startParameter.continueOnFailure = true

test.finalizedBy(aggregate, reports)

serenity {
    reports = ["single-page-html"]
}
